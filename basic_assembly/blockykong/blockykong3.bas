0 'BLKYKONG.BAS
10 CLS:GOSUB 1000
15 'NUMBER OF BARRELS
16 NB=3
17 'BARREL LOC AND RESTORE CHR
18 DIMBL(NB),BR(NB)
19 'BARREL START AND CHARACTER
20 BS=1024+(32*4)+9:BC=240+1
21 'MARIO START AND CHARACTER
22 MS=1024+(32*14)+6:MC=160+5

49 'LOCATIONS
50 ML=MS:FORA=0TONB:BL(A)=.:NEXT
60 MR=PEEK(ML):POKEML,MC
70 BR(0)=PEEK(BL(0)):POKEBL(0),BC
80 TM=100
100 FORA=.TONB:L=BL(A):IFL=.THEN160
110 P=PEEK(L+&H20):IFP=&H80 THENNL=L+&H20:GOTO145
115 IFA=1THENIFPEEK(L+&H40)=&HC9 THENNL=L+&H20:GOTO145
116 IFP=&HC9 THENNL=L+&H20:GOTO140
120 IFP=&HBD THENNL=L+1:GOTO140
130 IFP=&HBE THENNL=L-1:GOTO140
135 GOTO160
140 'IFPEEK(NL)=BC THEN160
145 POKEL,BR(A):IFNL>&H5DF THENBL(A)=.:GOTO160
155 BR(A)=PEEK(NL):POKENL,BC:BL(A)=NL
160 NEXT

500 A$=INKEY$:IFA$="A"THENNL=ML-1:GOTO530
510 IFA$="S"THENNL=ML+1:GOTO530
520 GOTO600
530 POKEML,MR:ML=NL:POKEML,MC

600 IFTM=.THENTM=4:GOTO620
610 TM=TM-1:GOTO100
620 FR=FR+1:IFFR=7THENFR=0:GOTO100
630 IFFR<5THENONFR GOSUB4000,4010,4020,4000
640 GOTO100

900 GOTO100

999 GOTO999

1000 '
1001 ' DRAW SCREEN
1002 '
1005 CLS0:PRINT@24,"BONUS";
1010 RESTORE:FORA=0TO15:READA$
1020 P=INSTR(A$,">"):IFP>0THENMID$(A$,P,1)=CHR$(&HBD):GOTO1020
1030 P=INSTR(A$,"<"):IFP>0THENMID$(A$,P,1)=CHR$(&HBE):GOTO1030
1040 P=INSTR(A$,"H"):IFP>0THENMID$(A$,P,1)=CHR$(&HC9):GOTO1040
1050 P=INSTR(A$,"U"):IFP>0THENMID$(A$,P,1)=CHR$(&H9F):GOTO1050
1060 P=INSTR(A$,"O"):IFP>0THENMID$(A$,P,1)=CHR$(&HFF):GOTO1060
1070 P=INSTR(A$," "):IFP>0THENMID$(A$,P,1)=CHR$(&H80):GOTO1070
1080 PRINT@32*A+2,A$;
1090 NEXT:RETURN

4000 PRINT@&H44,"  @  ";:PRINT@&H64," <=> ";:PRINT@&H84," / \ ";:RETURN
4010 PRINT@&H44,"@    ";:PRINT@&H64,"/=   ";:PRINT@&H84," !\  ";:RETURN
4020 PRINT@&H44,"    @";:PRINT@&H64,"   =\";:PRINT@&H84,"  /! ";
4030 FORA=.TONB:IFBL(A)>.THEN4040
4035 BL(A)=BS:BR(A)=PEEK(BS):POKEBS,BC:A=NB
4040 NEXT:RETURN

5000 DATA"       H H"
5010 DATA"       H H"
5020 DATA"    @  H H>>>>>>>"    
5030 DATA"OO /=\ H H      H"
5040 DATA"OO / \ H H      H"
5050 DATA">>>>>>>>>>>>>>>>>>>>>>>>>>"
5060 DATA"                       H" 
5070 DATA"  <<<<<<<<<<<<<<<<<<<<<<<<<<"
5080 DATA"     H     H"             
5090 DATA">>>>>>>>>>>>>>>>>>>>>>>>>>"
5100 DATA"         H     H       H"
5110 DATA"  <<<<<<<<<<<<<<<<<<<<<<<<<<"
5120 DATA"     H       H"
5130 DATA">>>>>>>>>>>>>>>>>>>>>>>>>>"
5140 DATA"  U                    H"
5150 DATA"<<<<<<<<<<<<<<<<<<<<<<<<<<<<"
